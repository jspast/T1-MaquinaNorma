let

	-- Definições --

	id = \x . x;
	auto = \a. a a;

	true = \a. \b. a;
	false = \a. \b. b;

	if = \a. \b. \c. a b c;
	not = \b. if b false true;
	and = \b1. \b2. if b1 b2 false;
	or = \b1. \b2. if b1 true b2;

	zero = \f. \x. x;
	succ = \n. (\A. \B. A (n A B));
	soma = \n1. \n2. (\A. \B. n1 A (n2 A B));
	mult = \n1. \n2. \A. \B. n1 (n2 A) B;
	exp = \n1. \n2. n2 n1;

	iszero = \n. n (\x. false) true;
	ispos = \n. not (iszero n);

	pair = \A B. \c. c A B;
	fst = \p. p true;
	snd = \p. p false;
	swap = \p. pair (snd p) (fst p);

	empty = \x.true;
	cons = pair;
	head = fst;
	tail = snd;
	isempty = \p. p (\a b.false);

	sI = \p. pair (snd p) (succ (snd p));
	pred = \n. fst (n sI (pair 0 0));
	sub = \n1. \n2. n2 pred n1;

	menor = \n1. \n2. ispos (sub n2 n1);

	Y = \f. (\x. f (x x)) (\x. f (x x));

	div = Y (\R. \a. \b. if (iszero b) 0 (if (menor a b) 0 (succ (R (sub a b) b))));
	mod = Y (\R. \a. \b. if (iszero b) 0 (if (menor a b) a (R (sub a b) b)));

	-- Exercícios --

	-- 1a --

	igual = \x y. if iszero (sub x y) (if iszero (sub y x) true false) false;

	-- 1b --

	pol = \x y. soma (exp x 2) (mult y 3);

	-- 1c --

	multTres = \x. if (iszero x) false (iszero (mod x 3));

	-- 1d --

	multFrac = \x y. (pair (mult (fst x) (fst y)) (mult (snd x) (snd y)));

	-- 1e --

	somaFrac = \x y. pair (soma (mult (fst x) (snd y)) (mult (snd x) (fst y))) (mult (snd x) (snd y));

	-- 1f --

	geraLista = Y (\R. \x. if (iszero x) empty (cons x (R (pred x))));

	-- 1g --

	multListaAux = Y (\M x. if (isempty x) 1 (mult (head x) (M (tail x))));

	multLista = \x. if (isempty x) 0 (multListaAux x);

	-- 1h --

	seqAux = Y (\R x y a. if (iszero a) y (R y (sub (mult 3 y) (mult 2 x)) (pred a)));

	seq = \x. if (iszero x) 2 (if (iszero (pred x)) 5 (seqAux 2 5 (pred x)));

in